plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'gl.ky'
version '1.0.0'

repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.0'
    runtimeOnly 'org.jetbrains.kotlin:kotlin-reflect:1.7.0'
    implementation 'com.typesafe:config:1.4.2'
    implementation 'com.sksamuel.hoplite:hoplite-core:2.1.5'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'org.bstats:bstats-base:2.2.1'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
}

sourceSets {
    main {
        kotlin {
            srcDir 'src/code'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

shadowJar {
    dependencies {
        include dependency('org.jetbrains.kotlin:kotlin-stdlib:1.7.0')
        include dependency('org.jetbrains.kotlin:kotlin-reflect:1.7.0')
        include dependency('com.typesafe:config:1.4.2')
        include dependency('com.sksamuel.hoplite:hoplite-core:2.1.5')
        include dependency('org.bstats:bstats-bukkit:2.2.1')
        include dependency('org.bstats:bstats-base:2.2.1')
    }
    relocate 'kotlin', 'gl.ky.kasumi.kt'
    relocate 'kotlinx', 'gl.ky.kasumi.ktx'
    relocate 'com.typesafe.config', 'gl.ky.kasumi.config'
    relocate 'com.sksamuel.hoplite', 'gl.ky.kasumi.hoplite'
    relocate 'org.bstats', 'gl.ky.kasumi.bstats'
    minimize()
}

build.dependsOn shadowJar
